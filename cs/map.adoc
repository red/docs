= Datový typ map!
:toc:
:numbered:
  

== Úvod

Struktura typu `map!` reprezentuje asociativní řadu (array) dvojic key/value. Poskytuje rychlý přístup pro čtení s interním použitím hashtable. Na rozdíl od datového typu `hash!` nepatří `map!` do skupiny `series!`, tudíž nezná koncept pozic (nevlastní interní iterátor). Koncepčně leží typ `map!` mezi datovými typy `hash!` a `object!`. Uživatelsky připomíná  slovník.

== Literálová skladba
----
#(<key> <value>...)

<key>  : hashovaný klíč typu scalar!, all-word!, any-string!
<value> : hodnota typu any-type!
----

Příklad:
----
#(a: 1 b: 2)
== #(
    a: 1
    b: 2
)
----

Je-li klíč typu *any-word*, je v mapě kvůli lepší přehlednosti konvertován na typ *set-word*. Nicméně, při dotazování na hodnotu klíčů, lze pro klíč z praktických důvodů (jednodušší zápis, zejména v cestách) použít jednoduchý tvar slova.
Podobně reflektor `keys-of` (popsaný v sekci Reflexe níže) vrací slova ve formátu `words` místo `set-words`, protože to zjednodušuje další zpracování (zejména shodování operací je snadnější se slovy než se set-slovy).

== Konstruktorová skladba

----
make map! <spec>

<spec> : tělo mapy neboli blok specifikací tvoří blok párů `key/value` nebo celočíselná hodnota
----

Příklad:
----
make map! [a 1 'b 2 "c" 3]
== #(
    a: 1
    b: 2
    "c" 3
)
----

Je-li _spec_ celé číslo, vytvoří se prázdný objekt typu `map!` s předurčeným počtem slotů (obvykle za účelem pozdějšího dynamického zaplnění mapy).

Příklad:
----
mp: make map! 3        ; == #()
lp: #()3               ; == #()

lp/a: true             ; == #(a: true)
type? lp/a             ; == logic!
----

Poznámky:
 
* Tělo mapy (blok specifikací) musí obsahovat *sudý* počet prvků, nemá-li být generována chyba. 
* Pro přístup ke klíči je možné použít prosté slovo (word) i jeho formu "set-word".
* Je-li jako hodnota zadáno `none` (typu none!), klíč se nevytvoří, případně existující klíč stejného jména se smaže. 
* Při vytváření mapy jsou všechny klíče hluboce kopírovány.
* Hodnoty typu series! se při vytváření mapy implicitně nekopírují; volba je ponechána na uživateli (což optimalizuje zdroje v běžných způsobech použití).
* hodnoty nejsou *redukovány*, tudíž pro některé speciální hodnoty (např. logic!) je vyžadována _konstrukční_ skladba.

Další způsob vytvoření nové mapy je kopírování existující:

----
cmp: copy mp
== #()
----

== Vyhledávání hodnot

Stejně jako hash! a block! je map! implicitně [underline]#case-preserving# a [underline]#case-insensitive#.

----
m: #(Ab: 2 aB: 5 ab: 10)
----
S použitím cesty:
----
<map>/<key>
get '<map>/<key>

m/ab             ; == 2  
get 'm/ab        ; == 2
----
S použitím výběrové funkce:
----
select <map> <key>

select m 'ab     ; == 2
----

Pro `case-sensitive` vyhledávání je zapotřebí uvést upřesnění `/case`:
----
get/case 'm/ab   ; == 10
select/case m aB ; == 5
----


Pokus o vyhledání v mapě nedefinovaného klíče vrací hodnotu `none`.


== Změna klíčů a hodnot

S použitím cest:
----
<map>/<key>: <value>                ; změna pouze hodnoty
set '<map>/<key> <value>            ; dtto

<map>      : slovo, odkazující na hodnotu `map!`
<key>      : slovo pro výběr hodnoty v mapě
<value>    : jakákoli hodnota
----

S použitím modifikačních akcí:
----   
put <map> <key> <value>             ; vložení dvojice
----

Provedením hromadných změn:
----
extend <map> <spec>                 ; rozšíření o dvojici                 

<spec> : blok s dvojicemi name/value (jednou či vícero)
----

Nativní funkce `extend` může přijmout více klíčů najednou, takže je vhodná pro hromadné změny.

Všechny tyto textové úpravy jsou 'case-insensitive'. Case-sensitive variantu způsobí upřesnění `/case`:
----
set/case '<map>/<key> <value>
put/case <map> <key> <value>
extend/case <map> <spec>
----

Poznámky: 

* zadání klíče, který dosud v mapě neexistuje, způsobí jeho vytvoření.
* přidání existujícího klíče změní jeho hodnotu, přičemž se implicitně provádí*case-insensitive* porovnávání.

Příklady:

----
m: #(Ab: 2 aB: 5 ab: 10)

m/a: true

set 'm/b false

put m 'Ab "hello"

set/case 'm/aB 0

set/case 'm/ab 192.168.0.1

print m
== #(
    Ab: "hello"
    aB: 0
    ab: 192.168.0.1
    a: true
    b: false
)
   
n: #(%cities.red 10)
extend n [%cities.red 99 %countries.red 7 %states.red 27]
m
== #(
	%cities.red 99
	%countries.red 7
	%states.red 27
)
----


== Mazání klíčů

Dvojici key/value jednoduše z mapy vymažeme přiřazením hodnoty `none`  ke klíči - s použitím jednoho z možných způsobů:

----
m: #(a: 1 b 2 "c" 3 d: 99)

m/b: none
put m "c" none
extend m [d #[none]]   ; "konstrukční syntaxe" hodnoty `none`

m
== #(
    a: 1
)
----

Je rovněž možné smazat všechny klíče najednou funkcí `clear`:
----
clear m
== #()
----

NOTE: U funkce `extend` je nutné použít takzvanou "construction syntax" aby se docílilo zadání `none` typu `none!` a nikoli typu `word!`.

Vložit slovo `none` v datovém typu `word!` lze pouze funkcí `extend`: 
----
extend m [a none]
nebo:
extend m #(b none)

type? m/a
== word!
type? m/b
== word!
----


== Reflexe

Pro práci s mapou (slovníkem) se s výhodou použijí další pomocné funkce:

* `find` ověří přítomnost klíče v mapě a vrátí `true`, byl-li nalezen, v opačném případě vrátí `none`.

 find #(a 123 b 456) 'b
 == true

* `length?` vrací počet dvojic `key/value` v mapě.

 length? #(a 123 b 456)
 == 2

* `keys-of` vrací seznam klíčů v mapě formou bloku (set-words are converted to words).

 keys-of #(a: 123 b: 456)
 == [a b]

* `values-of` vrací seznam hodnot v mapě.

 values-of #(a: 123 b: 456)
 == [123 456]

* `body-of` vrací všechny dvojice key/value v mapě.

 body-of #(a: 123 b: 456)
 == [a: 123 b: 456]
