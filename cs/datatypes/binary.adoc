= Binary! datatype
:toc:
:numbered:


== Úvodem

Hodnota `binary!` je tvořena řadou číselných znaků **zvolené** číselné soustavy - nikoliv jak název napovídá - pouze dvojkové soustavy.

Typ `binary!` je člen typesetů `default!` a `series!`

== Vytvoření

Hodnoty typu binary lze vytvořit s použitím literálové syntaxe nebo při runtime s použitím konstruktoru `make` nebo konverzí `to`.

== Literálová skladba

```
<binary-literal> ::=  2#{<base2-char>*} | 
                     16#{<hex-char>*} OR #{<hex-char>*} | 
                     64#{<base64-char>*}
```

Hodnota *base2* se skládá z jedné či více skupin osmi číslic `0-1`. Skupiny lze oddělovat mezerou

```red
>> 2#{11111111}
== #{FF}

>> 2#{11110000 00001111}
== #{F00F}
```

Hodnota *base16* je hexadecimální formát čísla a skládá se z jedné či více skupin dvou znaků `0-9 A-F a-f`
```red
>> 16#{2A}
== #{2A}

>> 16#{2A FF}
== #{2AFF}
```

Tento formát je implicitní a lze jej psát i bez označení báze.

```red
>> #{2A}
== #{2A}
```

Lichý počet znaků vyvolá chybu.

```red
>> 16#{2AF}
*** Syntax Error: invalid binary! at "#{2AF}"
*** Where: do
*** Stack: load 
```

Hodnota *base64* se musí skládat ze čtyř znaků nebo ze skupin čtyř znaků z množiny `0-9 A-Z a-z +/`. 

```red
>> 64#{2AQQ}
== #{D80410}

>> 64#{2A+/}
== #{D80FBF}
```

Méně než čtyři znaky nebo lichý počet znaků vyvolá chybu. Potřebnou délku lze vytvořit doplněním (maximálně) dvěma rovnítky `=`. 

```red
>> 64#{2A}
*** Syntax Error: invalid binary! at "64#{2A}"
*** Where: do
*** Stack: load 

>> 64#{2A2=}
== #{D80D}

>> 64#{D75q31==}
== #{0FBE6ADF}
```

== Deklarace pro runtime

*Použití* `make`:

```red
>> make binary! [42]
== #{2A}
```

*Konverze* `to`:

```red
>> to binary! 42
== #{0000002A}

>> to binary! [42]
== #{2A}
```

== Související funkce

=== enbase

```red
>> help enbase

USAGE:
     ENBASE value

DESCRIPTION: 
     Encodes a string into a binary-coded string (BASE-64 default). 
     ENBASE is a native! value.

ARGUMENTS:
     value        [binary! string!] "If string, will be UTF8 encoded."

REFINEMENTS:
     /base        => Binary base to use.
        base-value   [integer!] "The base to convert from: 64, 58, 16, or 2."
```

*Příklad*

```red
>> enbase #{0FBE6ADF}
== "D75q3w=="              ; báze 64 je pro funkci enbase implicitní

>> enbase/base #{0FBE6ADF} 2
== "00001111101111100110101011011111"

>> enbase "I"
== "SQ=="         
```

=== debase

```red
>> help debase

USAGE:
     DEBASE value

DESCRIPTION: 
     Decodes binary-coded string (BASE-64 default) to binary value. 
     DEBASE is a native! value.

ARGUMENTS:
     value        [string!] "The string to decode."

REFINEMENTS:
     /base        => Binary base to use.
        base-value   [integer!] "The base to convert from: 64, 58, 16, or 2."

```

*Příklad převodů tam a zpět:*

```red
>> enbase "I"
== "SQ==" 

>> enbase/base "I" 64
== "SQ=="

>> debase "SQ=="
== #{49}

>> to string! debase "SQ=="  
== "I"

Případně:

>> to-binary "I"
== #{49}

>> to string! #{49}
== "I"
```

== Komparace

Na hodnotu typu `binary!` lze aplikovat všechny komparační oprátory: `=, ==, <>, >, <, >=, &lt;=, =?`. Navíc jsou podporovány funkce `min` a `max`.


== Ověřování hodnot

Přímým dotazem:

```red
>> binary? #{2A} 
== true
```

Obecným dotazem:

```red
>> type? #{2A}
== binary!
```


== Předdefinovaná slova

=== Akce

`complement`, `or~`, `put`, `read`, `trim`, `write`, `xor~`

=== Funkce

`binary?`, `load`, `read-thru`, `save`, `to-binary`

=== Nativa

`call`, `checksum`, `debase`, `decompress`, `enbase`, `parse`

=== Operátory

`and`, `or`, `xor`